<?xml version="1.0" encoding="utf-8"?>
<comp:ResizableTitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009"
						   xmlns:s="library://ns.adobe.com/flex/spark"
						   xmlns:mx="library://ns.adobe.com/flex/mx"
						   xmlns:comp="comp.*"
						   width="100%" height="100%" close="wizardWindow_closeHandler(event)"
						   creationComplete="creationCompleteHandler(event)">
	
	<fx:Metadata>
		[Event(name="finish", type="comp.WizardEvent")]
	</fx:Metadata>
	
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			private var _context:Object;
			
			private var wizardPages:Array = new Array();
			
			[Bindable]
			private var navModel:ArrayCollection = new ArrayCollection();
			
			public function get context():Object
			{
				return _context;
			}

			public function set context(value:Object):void
			{
				_context = value;
			}

			protected function wizardWindow_closeHandler(event:CloseEvent):void
			{
				close();
			}
			
			protected function btnCancel_clickHandler(event:MouseEvent):void
			{
				close();
			}
			
			private function close():void
			{
				PopUpManager.removePopUp(this);
			}
			
			public function addWizardPages(_wizardPages:Array):void{
				
				for each(var wizardPage:WizardPage in _wizardPages){
					var navObj:Object = wizardPage.navLabel;
					navObj.index = wizardPages.length + 1;
					
					if(wizardPages.length == 0){
						navObj.selected = true;
						navObj.checked = true;
					}else{
						navObj.selected = false;
						navObj.checked = false;
					}
					
					wizardPage.addEventListener(WizardEvent.NEXT_VALID, nextHandler);
					wizardPage.addEventListener(WizardEvent.BACK_VALID, backHandler);
					
					wizardPages.push(wizardPage);
					
				}
			}
			
			protected function btnFinish_clickHandler(event:MouseEvent):void
			{
				var wizardEvent:WizardEvent = new WizardEvent(WizardEvent.FINISH);
				// TODO set finish data
				wizardEvent.data = {};
				dispatchEvent(wizardEvent);
				
				callbackForFinish();
			}
			
			protected function callbackForFinish():void{
				// override by sub-class.
			}
			
			protected function callbackForNext():void{
				// override by sub-class.
			}
			
			protected function callbackForBack():void{
				// override by sub-class.
			}
			
			private function backHandler(evt:WizardEvent):void{
				var wizardPage:WizardPage = pageContainer.removeElementAt(0) as WizardPage;
				var index:int = wizardPages.indexOf(wizardPage);
				
				if(index > 0){
					var backWizardPage:WizardPage = wizardPages[index - 1];
					backWizardPage.navLabel.selected = true;
					backWizardPage.navLabel.checked = true;
					
					wizardPage.navLabel.selected = false;
					
					pageContainer.addElement(backWizardPage);
					
					ArrayCollection(this.listItem.dataProvider).refresh();
					
					if(index == wizardPages.length - 1){
						btnFinish.enabled = false;
						btnNext.enabled = true;
					}
					
					if(index == 1){
						btnBack.enabled = false;
					}
				}
			}
			
			private function nextHandler(evt:WizardEvent):void{
				
				if(evt.data.isValid){
					var wizardPage:WizardPage = pageContainer.removeElementAt(0) as WizardPage;
					var index:int = wizardPages.indexOf(wizardPage);
					
					if(index < wizardPages.length - 1){
						
						var nextWizardPage:WizardPage = wizardPages[index + 1];
						nextWizardPage.navLabel.selected = true;
						nextWizardPage.navLabel.checked = true;
						
						wizardPage.navLabel.selected = false;
						
						pageContainer.addElement(nextWizardPage);
						
						ArrayCollection(this.listItem.dataProvider).refresh();
						
						if(index == wizardPages.length - 2){
							btnFinish.enabled = true;
							btnNext.enabled = false;
						}
						
						if(index == 0){
							btnBack.enabled = true;
						}
					}
				}else{
					// TODO show error message.
					var msgArr:Array = evt.data.error;
					Alert.show(msgArr.join("\n"));
				}
			}
			
			
			protected function btnNext_clickHandler(event:MouseEvent):void
			{
				var wizardEvent:WizardEvent = new WizardEvent(WizardEvent.NEXT);
				wizardEvent.data = {};
				
				for each(var wizardPage:WizardPage in wizardPages){
					if(wizardPage.navLabel.selected){
						wizardPage.dispatchEvent(wizardEvent);
						break;
					}
				}
				
				dispatchEvent(wizardEvent);
				
				callbackForNext();
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				this.setStyle("skinClass", ResizableTitleWindowSkin);
				
				if(wizardPages.length > 0){
					pageContainer.addElement(wizardPages[0]);
					
					for each(var wizardPage:WizardPage in wizardPages){
						ArrayCollection(listItem.dataProvider).addItem(wizardPage.navLabel);
					}
					
					ArrayCollection(listItem.dataProvider).refresh();
				}
				
				this.addEventListener(WizardEvent.NAV_LABEL_CLICK,function(evt:WizardEvent):void{
					
					var dataProvider:ArrayCollection = listItem.dataProvider as ArrayCollection;
					
					for each(var item:Object in dataProvider){
						if(item !== evt.data && item.selected){
							item.selected = false;
							break;
						}
					}
					
					dataProvider.refresh();
					
					for each(var wizardPage:WizardPage in wizardPages){
						if(wizardPage.navLabel.selected){
							pageContainer.removeElementAt(0);
							pageContainer.addElement(wizardPage);
							
							var index:int = wizardPages.indexOf(wizardPage);
							
							if(index == 0){
								btnBack.enabled = false;
								btnNext.enabled = true;
								btnFinish.enabled = false;
							}else if(index == wizardPages.length - 1){
								btnBack.enabled = true;
								btnNext.enabled = false;
								btnFinish.enabled = true;
							}else{
								btnBack.enabled = true;
								btnNext.enabled = true;
								btnFinish.enabled = false;
							}
							
							wizardPage.dispatchEvent(evt);
							
							break;
						}
					}
				});
			}
			
			protected function btnBack_clickHandler(event:MouseEvent):void
			{
				var wizardEvent:WizardEvent = new WizardEvent(WizardEvent.BACK);
				wizardEvent.data = {};
				
				for each(var wizardPage:WizardPage in wizardPages){
					if(wizardPage.navLabel.selected){
						wizardPage.dispatchEvent(wizardEvent);
						break;
					}
				}
				
				dispatchEvent(wizardEvent);
				
				callbackForBack();
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		
	</fx:Declarations>
	<comp:controlBarContent>
		<s:Spacer width="100%"/>
		<s:Button id="btnBack" label="Back" click="btnBack_clickHandler(event)" enabled="false"/>
		<s:Button id="btnNext" label="Next" click="btnNext_clickHandler(event)"/>
		<s:Button id="btnFinish" label="Finish" click="btnFinish_clickHandler(event)" enabled="false"/>
		<s:Button id="btnCancel" label="Cancel" click="btnCancel_clickHandler(event)"/>
	</comp:controlBarContent>
	
	<s:HGroup width="100%" height="100%" contentBackgroundColor="#EFEFEF" gap="0">
		<s:BorderContainer width="200" height="100%" borderVisible="false"
						   contentBackgroundColor="#EFEFEF">
			<s:List id="listItem" width="100%" height="100%" borderVisible="false"
					contentBackgroundColor="#EFEFEF" dataProvider="{navModel}"
					rollOverColor="#EFEFEF" selectionColor="#EFEFEF">
				<s:itemRenderer>
					<fx:Component>
						<comp:WizardNavItem/>
					</fx:Component>
				</s:itemRenderer>
			</s:List>
		</s:BorderContainer>
		<s:BorderContainer id="pageContainer" width="100%" height="100%" borderVisible="false">
			<s:layout>
				<s:VerticalLayout/>
			</s:layout>
		</s:BorderContainer>
	</s:HGroup>
</comp:ResizableTitleWindow>
